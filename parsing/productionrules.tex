\begin{grammar}
<Start> ::= <Constant>
	\alt '=' <Formula>
	\alt <ArrayFormula>
	
<ArrayFormula> ::= `\{=' <Formula> `\}'

<Formula> ::= <Constant>
         \alt <Reference>
         \alt <FunctionCall>
         \alt `(' <Formula> `)'
         \alt <ConstantArray>
         \alt "RESERVED-NAME"
         
<Constant> ::= "NUMBER" | "STRING" | "BOOL" | "ERROR"
%         \alt "STRING"
%         \alt "BOOL"
%         \alt "ERROR"
         
<FunctionCall> ::= <Function> <Arguments> `)'
		\alt <UnOpPrefix> <Formula>
		\alt <Formula> `\%'
		\alt <Formula> <BinOp> <Formula>
		
<UnOpPrefix> = `+' | `-'

<BinOp> = `+' | `-' | `*' | `/' | `\textasciicircum'
	\alt `<' | `>' | `=' | `<=' | `>=' | `<>'

<Function> ::= "FUNCTION" | "UDF"
	
<Arguments> ::= <Argument>
	\alt <Argument> `,' <Arguments>

<Argument> ::= <Formula> | $\epsilon$

<Prefix> ::= "SHEET"
	\alt "FILE" "SHEET"
	\alt "FILE" `!'
	\alt "QUOTED-FILE-SHEET"
	\alt "MULTIPLE-SHEETS"
	\alt "FILE" "MULTIPLE-SHEETS"

\columnbreak

<Reference> ::= <ReferenceItem>
	\alt <Reference> `:' <Reference>
	\alt <Reference> `\ ' <Reference>
	\alt `(' <Union> `)'
	\alt `(' <Reference> `)' 
	\alt <Prefix> <ReferenceItem>
    \alt <Prefix> "UDF" <Arguments> `)'
    \alt <DynamicDataExchange>
    
<ReferenceItem> ::= "CELL"
	\alt <NamedRange>
	\alt "REFERENCE-FUNCTION" Arguments `)'
	\alt "VERTICAL-RANGE"
	\alt "HORIZONTAL-RANGE"
	\alt "ERROR-REF"
	
<NamedRange> ::= "NAMED-RANGE"
            \alt "NAMED-RANGE-PREFIXED"

<Union> ::= <Reference> | <Reference> `,' <Union>
	
<DynamicDataExchange> ::= "FILE" `!' "DDECALL"

<ConstantArray> ::= `\{' <ArrayColumns> `\}'

<ArrayColumns> ::= <ArrayRows>
	\alt <ArrayRows> `;' <ArrayColumns>

<ArrayRows> ::= <ArrayConstant>
	\alt <ArrayConstant> `,' <ArrayRows>
	
<ArrayConstant> ::= <Constant>
	\alt <UnOpPrefix> "NUMBER"
	\alt "ERROR-REF"

\end{grammar}